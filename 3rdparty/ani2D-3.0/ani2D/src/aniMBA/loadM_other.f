C ======================================================================
      Subroutine loadMgmv(nP, nPv, MaxP, XYP, lbP, fixP,
     &                    nF, nFv, MaxF, IPF, lbF, fixF,
     &                    nC,            Crv, lbC, 
     &                    nE, nEv, MaxE, IPE, lbE, fixE,
     &                    fName)
C ======================================================================
C Routines read the input mesh from single file fName.gmv.
C ======================================================================
      implicit none
C group (M)
      Integer  nP, nPv, MaxP, lbP(*), fixP(*)
      Real*8   XYP(2, *)

      Integer  nF, nFv, MaxF, IPF(2, *), lbF(*), fixF(*)

      Integer  nC, lbC(*)
      Real*8   Crv(2, *)

      Integer  nE, nEv, MaxE, IPE(3, *), lbE(*), fixE(*)

      Character*(*) fName


C LOCAL VARIABLES
      Character*30  fNameExt, keyword
      Integer  i, j, m, n, nCrv

C ======================================================================
      fNameExt = fName // '.gmv'

      Write(*,'(A,A)') 'Loading mesh ', fNameExt

      Open(10, file=fNameExt, status='OLD', ERR=1000)
      Read(10,*) 
      Read(10,*) 
      Read(10,*) 

      Read(10,*) keyword, nP
      If(nP.GT.MaxP) Call errMesMBA(1003, 'loadMgmv',
     &                          'local parameter MaxP is small')
      Do n = 1, nP
         Read(10,*) (XYP(j, n), j=1,2)
      End do


c ... reading the boundary edges: 
      nF = 0
      If(nF.GT.MaxF) Call errMesMBA(1004, 'loadMgmv',
     &                          'local parameter MaxF is small')


c ... reading the elements: 
      Read(10,*) keyword, nE
      If(nE.GT.MaxE) Call errMesMBA(1006, 'loadMgmv',
     &                          'local parameter MaxE is small')
      Do n = 1, nE
         Read(10,*) keyword, m, (IPE(j, n), j=1,3)
      End do


c ... reading parametrization of the curved boundary edges:
      nCrv = 0
      If(nCrv.GT.MaxF) Call errMesMBA(1004, 'loadMgmv',
     &                            'local parameter MaxF is small')


c ... reading the fixed mesh points, faces and elements
      nPv = 0 
      nFv = 0 
      nEv = 0 

      Close(10)

      Return

 1000 Continue
      Call errMesMBA(4001, 'loadMgmv', 'Input file name is wrong ')

      Return
      End



C ======================================================================
      Subroutine loadMTriangle(nP, nPv, MaxP, XYP, lbP, fixP,
     &                         nF, nFv, MaxF, IPF, lbF, fixF,
     &                         nC,            Crv, lbC, 
     &                         nE, nEv, MaxE, IPE, lbE, fixE,
     &                         fName)
C ======================================================================
C  Reads the input mesh from files generated by Triangle software.
C  fName may have any non-empty extension.
C
C  This is the experimental code.
C ======================================================================
      implicit none
C group (M)
      Integer  nP, nPv, MaxP, lbP(*), fixP(*)
      Real*8   XYP(2, *)

      Integer  nF, nFv, MaxF, IPF(2, *), lbF(*), fixF(*)

      Integer  nC, lbC(*)
      Real*8   Crv(2, *)

      Integer  nE, nEv, MaxE, IPE(3, *), lbE(*), fixE(*)

      Character*(*) fName

C LOCAL VARIABLES
      Character*30  fNameExt
      Integer  i, j, k, n, nAtrb, nBmrk

C ======================================================================
      i = 1
      Do while(fName(i:i+4) .NE. '.node' .AND. 
     &         fName(i:i+3) .NE. '.ele')
         i = i + 1
      End do

c ... reading fName.ele file
      fNameExt = fName(1:i) // 'ele'
      Write(*,'(A,A)') 'Loading mesh ', fNameExt

      Open(10, file=fNameExt, status='OLD', ERR=1000)

      Read(10,*) nE, k, nAtrb
      Do n = 1, nE
         Read(10,*) k, (IPE(j, n), j = 1, 3)
      End do

      Close(10)


c ... reading fName.node file
      fNameExt = fName(1:i) // 'node'
      Write(*,'(A,A)') 'Loading mesh ', fNameExt

      Open(10, file=fNameExt, status='OLD', ERR=1000)

      Read(10,*) nP, k, nAtrb, nBmrk
      Do n = 1, nP
         Read(10,*) k, (XYP(j, n), j = 1, 2)
      End do

      Close(10)

c ... unsupported parameters
      nF  = 0
      nC  = 0
      nPv = 0
      nFv = 0
      nEv = 0

      Do n = 1, nE
         lbE(n) = 1
      End do

      Return

 1000 Continue
      Call errMesMBA(4001, 'loadMTriangle', 'Input file name is wrong')

      Return
      End


